reset
set terminal pngcairo size 1280,960
set xlabel "temperature [K]"
set ylabel "pressure [MPa]"
set cblabel "density [kg/m3]"
set ytics format "%.0e"
set cbtics format "%.0e"
set logscale y
set output "lines.png"
set key bottom
set title "Phases boundaries, according to IAPWS-R7 and R14"
set xrange [173:773]
set yrange [1e-6:1e6]

set object circle center 251.165,208.556 size 3
set label "Triple point L-Ih-III: 251.165 K, 208.566 MPa" at 280,150 left
set arrow from 280,150 to 255,208

set object circle center 256.164,350.1 size 3
set label "Triple point L-III-V: 273.31 K, 632.4 MPa" at 300,400 left
set arrow from 300,400 to 260,350.1

set object circle center 273.31,632.4 size 3
set label "Triple point L-V-VI: 273.31 K, 632.4 MPa" at 270,2e4 center
set arrow from 270,1.5e4 to 273,800

set object circle center 355,2216 size 3
set label "Triple point L-VI-VII: 355 K, 2216 MPa" at 355,1300 left

set object circle center 647.096,22.064 size 3
set label "Critical point: 647.096 K, 22.064 MPa" at 647.096,40 center

set object circle center 273.16,611.657e-6 size 3
set label "Triple point S-L-G: 273.16 K, 611.657 Pa" at 280,500e-6 left

plot                                                                                 \
      "sublimation.dat" using 1:($2*1e-6) w l t "sublimation line"                   \
    , "saturation.dat"  using 1:($2*1e-6) w l t "saturation line"                    \
    , "melting_ih.dat"  using 1:($2*1e-6) w l t "melting line Ih"                    \
    , "melting_iii.dat" using 1:($2*1e-6) w l t "melting line III"                   \
    , "melting_v.dat"   using 1:($2*1e-6) w l t "melting line V"                     \
    , "melting_vi.dat"  using 1:($2*1e-6) w l t "melting line VI"                    \
    , "melting_vii.dat" using 1:($2*1e-6) w l t "melting line VII"

# vim:ft=gnuplot
